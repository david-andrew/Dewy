//http://www.engr.mun.ca/~theo/Courses/fm/pub/context-free.pdf
//https://www.w3.org/TR/xml/#sec-notation
//https://en.wikipedia.org/wiki/Whitespace_character

//Most of these should be purely character sets...

#eps = 'ϵ' | '\\e' | "''" | '""';                           // ϵ, \e, '', or "" indicates empty element, i.e. nullable
//#wschar = [\x9-\xD\x20\x85\xA0\x1680\x2000-\x200A\x2028\x2029\x202F\x205F\x3000]; //ascii + unicode whitespace chars
#wschar = [\x9-\xD\x20];                                    // ascii whitespace characters.
#line_comment = '/' '/' (\U - '\n')* '\n';                  // single line comment
#block_string = \U* - \U* '}/';                             // inside of a block comment. Cannot end with block comment delimiter
#block_comment = '/' '{' (#block_comment | #block_string)* '}/';       // block comment, with allowed nested block comments
#ws = (#wschar | #line_comment | #block_comment)*;          // optional whitespace sequence
#anyset = '\\' [uUxX] | [VUξ];                              // V, U, ξ, \U, \u, \X, or \x used to indicate any unicode character
#hex = '\\' [uUxX] [0-9a-fA-F]+ / [0-9a-fA-F];              // hex number literal. Basically skipping the number part makes it #any
#number = [0-9]+ / [0-9];                                   // decimal number literal. Used to indicate # of repetitions
#charsetchar = \U - [\-\[\]] - #wschar;                     // characters allowed in a set are any unicode excluding '-', '[', or ']', and whitespace
#item = #charsetchar | #escape | #hex;                      // items that make up character sets, i.e. raw chars, escape chars, or hex chars
#charset = '[' (#ws #item (#ws '-' #ws #item)? #ws)+ ']';   // set of chars specified literally. Whitespace is ignored, and must be escaped.

//paired grouping operators
#group = '(' #ws #expr #ws ')';                             // group together/force precedence
#capture = '{' #ws #expr #ws '}';                           // group to capture
#char = '"' (\U - '"' | #escape | #hex) '"';                // "" single character
#char = "'" (\U - "'" | #escape | #hex) "'";                // '' single character
#string = '"' (\U - '"' | #escape | #hex)2+ '"';            // "" string of 2+ characters
#string = "'" (\U - "'" | #escape | #hex)2+ "'";            // '' string of 2+ characters
#escape = '\\' \U;                                          // an escape character. Recognized escaped characters are \n \r \t \v \b \f \a. 
                                                            // all others just put the second character literally. Common literals include \\ \' \" \[ \] \-

//post operators
#star = #expr #ws (#number)? #ws '*';                       // zero or (number or more)
#plus = #expr #ws (#number)? #ws '+';                       // (number or one) or more 
#option = #expr #ws '?';                                    // optional
#count = #expr #ws #number;                                 // exactly number of
#compliment = #set #ws '~';                                 // compliment of. equivalent to #any - #set

//implicit operators
#cat = #expr (#ws #expr)+;                                  // concatenate left and right

//binary expr operators
#or = (#expr #ws '|' #ws #expr) - #union;                   // left or right expression
#reject = (#expr #ws '-' #ws #expr) - #diff;                // reduce left expression only if it is not also the right expression
#nofollow = #expr #ws '/' #ws #expr;                        // reduce left expression only if not followed by right expression
#greaterthan = #expr #ws '>' #ws #expr;                     // left expression has higher precedence than right expression
#lessthan = #expr #ws '<' #ws #expr;                        // left expression has lower precedence than right expression

//binary set operators
#diff = #set #ws '-' #ws #set;                              // everything in left that is not in right
#intersect = #set #ws '&' #ws #set;                         // intersect of left and right. TODO->consider if we should add this as parser filter, rather than just restrict to sets
#union = #set #ws '|' #ws #set;                             // union of left and right

//syntax constructs
#set = #anyset | #char | #hex | #charset | #compliment | #diff | #intersect | #union;
#expr = #eps | #set | #group | #capture | #string | #star | #plus | #option | #count 
    | #cat | #or | #greaterthan | #lessthan | #reject | #nofollow | #identifier;
#hashtag = '#' [a-zA-Z] [a-zA-Z0-9~!@#$&_?]* / [a-zA-Z0-9~!@#$&_?];
#rule = #hashtag #ws '=' #ws #expr #ws ';';
#grammar = (#ws #rule)* #ws;
#start = #grammar;

//#update_parser_table(#start)


/{Interesting notes

`((A)2)+` indicates `A` exactly 2, 4, 6, 8, ... times
while `(A)2+` indicates `A` exactly 2, 3, 4, 5, ... times
Also `((A)2)*` indicates `A` exactly 0, 2, 4, 6, ... times
`(A)5+ - (A)11+` indicates repeat `A` exactly 5 to 10 times

`A+?` is the same as `A*`. Same with `(A)2+?` and `(A)2*`
Also `A?` is equivalent to (and represented internally as) `A | ϵ`
Also meaning that `A*` is represented as `A+ | ϵ`
`rule = A+` `__A = __A A | ϵ`, `rule = __A A`?

`(A)5+` indicates 5 or more of `A` while `(A)5*` indicates 0 or 5 or more of `A`

all expressions that reduce to a #set are combined / treated as a single unicode character set

all whitespace in charsets must either be escaped or in hex


}/

//e.g. the range expression in dewy
//range has no associativity, i.e. subexpressions may not be ranges themselves

#bare_range = #expr - #range ':' #expr - #range;
#range = #bare_range | [(\[] #bare_range [)\]];

/{
Precedence levels
//TODO...
//probably no associativity to worry about

//0 - identifier
0 - group
0 - capture
//0 - charset
//0 - string
//0 - anyset
//0 - eps
//0 - hex

1 - star
1 - plus
1 - option
1 - compliment
1 - count

2 - concatenation

3 - diff
3 - intersect
3 - reject
3 - nofollow

4 - union
4 - or
4 - greater than
4 - less than



#what about restrictions / disambiguation filters?
- follow (no follow | must follow)
- reject
- prefer
- precedence level + associativity

//// Raw-ish syntax for rnglr tree filters ////

//Follow restriction (e.g. force longest matched identifier)
#I = [A-Za-z][A-Za-z0-9_]*;
#I -/- [A-Za-z0-9_]; //also follow restriction could be whole strings/longer

//Reject filter (e.g. reserved words are not identifiers)
#I = [A-Za-z][A-Za-z0-9_]*;
#I = "public"{reject};

//prefer / avoid filters (i.e. multiple ambiguous overlapping trees, which to prefer/avoid )
#I = [A-Za-z][A-Za-z0-9_]*;
#I = "public"{prefer}

//priority filter (i.e. precedence and associativity)
#E = #E '->' #E{right} > #E = #E "or" #E{left};


//// Proposed alterations of each ////

//Follow restriction
#I = [A-Za-z][A-Za-z0-9_]* / [A-Za-z0-9_];
#rule = #body_expr / #non_follow_expr;

//Reject filter
#I = [A-Za-z][A-Za-z0-9_]* - "public";
#rule = #body_expr - #reject_expr;

//prefer/avoid filter
//TODO...
//useful for e.g. C++ where the "most vexing parse" is hard to specify with a normal CFG

//priority filter
//for precedence level, I think we'll have `<` `>` and they go in place of `|` 
//associativity is handled via exclusion

//defining expressions
#addition = #expr '+' #expr - #addition;                // left associativity
#subtraction = #expr '-' #expr - #subtraction;          // left associativity
#multiplication = #expr '*' #expr - #multiplication;    // left associativity
#division = #expr '/' #expr - #division;                // left associativity
#exponential = #expr - #exponential '^' #expr;          // right associativity
#value = [0-9]+ ('.' [0-9]+)? ([eE] [+\-]? [0-9]+ ('.' [0-9]+)?)? / [0-9];

//setting precedence levels
#expr = #exponential > #multiplication | #division > #addition | #subtraction;
#expr = #value; //precedence doesn't really matter here, but this acts just like an `| #value` on the end of the previous production
}/