// example of disambiguating precedence (and associativity) in mathematical expressions
#expr = #pow > #jux > #mul | #div | #mod > #add | #sub > #atom;
#add = #expr #w* '+' #w* (#expr - (#add | #sub));
#sub = #expr #w* '-' #w* (#expr - (#add | #sub));
#jux = #atom #s* #atom;
#mul = #expr #w* '*' #w* (#expr - (#mul | #div | #mod));
#div = #expr #w* '/' #w* (#expr - (#mul | #div | #mod));
#mod = #expr #w* '%' #w* (#expr - (#mul | #div | #mod));
#pow = (#expr - #pow) #w* '^' #w* #expr;

#atom = #group | #id | #num;
#group = '(' #w* #expr #w* ')';
#id = [A-Za-z_] [0-9A-Za-z_]* / [0-9A-Za-z_];
#num = [0-9]+;

#w = [\n\x20];  // all whitespace
#s = [\x20];    // non-line-breaking whitespace